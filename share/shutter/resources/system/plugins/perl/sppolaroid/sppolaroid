#! /usr/bin/env perl

###################################################
#
#  Copyright (C) 2008-2012 Mario Kemper <mario.kemper@googlemail.com> and Shutter Team
#
#  This file is part of Shutter.
#
#  Shutter is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 3 of the License, or
#  (at your option) any later version.
#
#  Shutter is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with Shutter; if not, write to the Free Software
#  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
#
###################################################

use utf8;
use strict;
use warnings;
use Gtk2 '-init';
use Image::Magick;
use POSIX qw/setlocale strftime/;
use Locale::gettext;
use Glib qw/TRUE FALSE/;
use FindBin '$Bin';    #path where plugin is located
use File::Temp qw/ tempfile tempdir /;
use Time::HiRes qw/usleep/;

#load modules at custom path at runtime
#--------------------------------------
require lib;
import lib $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/modules";

#proc (Thanks to Michael Schilli)
require Proc::Simple;

#load shutter's modules
#--------------------------------------
require Shutter::App::SimpleDialogs;

#Set LC_NUMERIC to C to prevent decimal commas (or anything else)
setlocale(LC_NUMERIC, "C");	
#configure gettext using ENV Variable (setup during shutter start)
setlocale( LC_MESSAGES, "" );
my $d = Locale::gettext->domain("shutter-plugins");
$d->dir( $ENV{'SHUTTER_INTL'} );

#icontheme to determine if icons exist or not
#in some cases we deliver fallback icons
my $icontheme = Gtk2::IconTheme->get_default;
$icontheme->append_search_path($ENV{'SHUTTER_ROOT'} . "/share/icons");

#shutter will ask for some infos
my %plugin_info = 	( 	'name'	=> $d->get( "Polaroid" ),
						'sort'	=> $d->get( "Effect" ),
						'tip'	=> $d->get("Make your screenshot look like a polaroid photo, add a caption, and even rotate it a little"),
					);

binmode( STDOUT, ":utf8" );
if ( exists $plugin_info{$ARGV[ 0 ]} ) {
	print $plugin_info{$ARGV[ 0 ]};
	exit;
}

#these variables are passed to the plugin
my $socket_id = $ARGV[ 0 ];
my $filename  = $ARGV[ 1 ];
my $width     = $ARGV[ 2 ];
my $height    = $ARGV[ 3 ];
my $filetype  = $ARGV[ 4 ];

#decode filename
utf8::decode $filename;

#check convert command
my $use_caption = FALSE;
if ( `convert --help | grep caption` =~ /caption/ ) {
	$use_caption = TRUE;
}

my $plug = Gtk2::Plug->new( $socket_id );
$plug->set_default_icon_name( 'shutter' );
$plug->set_border_width( 10 );

$plug->signal_connect( destroy => sub { Gtk2->main_quit } );

#tooltips
my $tooltips = Gtk2::Tooltips->new;

#configure buttons and other needed controls
my $caption_label = Gtk2::Label->new( $d->get( "Text:" ) );
my $caption_entry = Gtk2::Entry->new();
$caption_entry->set_text( "%c - $filename" );

$tooltips->set_tip(
	$caption_label,
	$d->get(
		"There are several wild-cards available, like\n%Y = year\n%m = month\n%d = day\n%T = time"
	)
);
$tooltips->set_tip(
	$caption_entry,
	$d->get(
		"There are several wild-cards available, like\n%Y = year\n%m = month\n%d = day\n%T = time"
	)
);

my $pointsize_label = Gtk2::Label->new( $d->get( "Font size:" ) );
my $pointsize_sbutton = Gtk2::SpinButton->new_with_range( 1, 200, 1 );
$pointsize_sbutton->set_value( 20 );

my $gravity_label = Gtk2::Label->new( $d->get( "Gravity:" ) );
my $gravity_combo = Gtk2::ComboBox->new_text;
$gravity_combo->insert_text( 0, "NorthWest" );
$gravity_combo->insert_text( 1, "North" );
$gravity_combo->insert_text( 2, "NorthEast" );
$gravity_combo->insert_text( 3, "West" );
$gravity_combo->insert_text( 4, "Center" );
$gravity_combo->insert_text( 5, "East" );
$gravity_combo->insert_text( 6, "SouthWest" );
$gravity_combo->insert_text( 7, "South" );
$gravity_combo->insert_text( 8, "SouthEast" );
$gravity_combo->set_active( 4 );

my $angle_label = Gtk2::Label->new( $d->get( "Rotation:" ) );
my $angle_sbutton = Gtk2::SpinButton->new_with_range( -90, 90, 1 );
$angle_sbutton->set_value( -5 );

my $stroke_color_label = Gtk2::Label->new( $d->get("Stroke color"). ":" );
my $stroke_color = Gtk2::ColorButton->new();
$stroke_color->set_color( Gtk2::Gdk::Color->parse('black') );
$stroke_color->set_alpha( int( 1.0 * 65535 ) );
$stroke_color->set_use_alpha(TRUE);
$stroke_color->set_title( $d->get("Choose stroke color") );

#we define two Gtk2::Image widgets
#to store the screenshot
#and a throbber that is shown while the changes are processed
my $preview =
	Gtk2::Image->new_from_pixbuf(
	   Gtk2::Gdk::Pixbuf->new_from_file_at_scale( $filename, 300, 300, TRUE ) );

my $preview_throb =
	Gtk2::Image->new_from_file( $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/icons/throbber.gif" );

my $sg = Gtk2::SizeGroup->new ('both');
$sg->set ('ignore-hidden' => FALSE);
$sg->add_widget($preview);
$sg->add_widget($preview_throb);


#we define three Gtk2::Button widgets
#to refresh, save and cancel the plugin's work
my $refresh_btn = Gtk2::Button->new_from_stock( 'gtk-refresh' );
$refresh_btn->signal_connect( 'clicked', \&fct_imagemagick_polaroid,
							  'refresh' );

my $save_btn = Gtk2::Button->new_from_stock( 'gtk-save' );
$save_btn->signal_connect( 'clicked', \&fct_imagemagick_polaroid, 'save' );

my $cancel_btn = Gtk2::Button->new_from_stock( 'gtk-cancel' );
$cancel_btn->signal_connect( 'clicked' => sub { Gtk2->main_quit; exit 2; }, 'cancel' );

#define the gui layout
my $vbox_param = Gtk2::VBox->new( TRUE,  10 );
my $hbox1      = Gtk2::HBox->new( FALSE, 10 );
my $hbox2      = Gtk2::HBox->new( FALSE, 10 );
my $hbox3      = Gtk2::HBox->new( FALSE, 10 );
my $hbox4      = Gtk2::HBox->new( FALSE, 10 );
my $hbox5      = Gtk2::HBox->new( FALSE, 10 );

my $hbox_row1 = Gtk2::HBox->new( TRUE, 10 );
my $hbox_row2 = Gtk2::HBox->new( TRUE, 10 );

my $hbox_btn  = Gtk2::HBox->new( TRUE,  10 );
my $vbox_btn  = Gtk2::VBox->new( FALSE, 10 );
my $vbox_main = Gtk2::VBox->new( FALSE, 10 );

#packing
if ( $use_caption ) {
	$hbox1->pack_start( $caption_label, FALSE, TRUE, 5 );
	$hbox1->pack_start( $caption_entry, TRUE,  TRUE, 5 );

	$hbox2->pack_start( $gravity_label, FALSE, TRUE, 5 );
	$hbox2->pack_start( $gravity_combo, TRUE,  TRUE, 5 );

	$hbox3->pack_start( $pointsize_label,   FALSE, TRUE, 5 );
	$hbox3->pack_start( $pointsize_sbutton, TRUE,  TRUE, 5 );

	$hbox4->pack_start( $angle_label,   FALSE, TRUE, 5 );
	$hbox4->pack_start( $angle_sbutton, TRUE,  TRUE, 5 );

	$hbox5->pack_start( $stroke_color_label, FALSE, TRUE, 5 );
	$hbox5->pack_start( $stroke_color, FALSE, TRUE, 5 );

	$hbox_row1->pack_start_defaults( $hbox1 );
	$hbox_row1->pack_start_defaults( $hbox2 );

	$hbox_row2->pack_start_defaults( $hbox3 );
	$hbox_row2->pack_start_defaults( $hbox4 );
	$hbox_row2->pack_start_defaults( $hbox5 );

	$vbox_param->pack_start( $hbox_row1, TRUE, TRUE, 5 );

} else {
	$hbox4->pack_start( $angle_label,   FALSE, TRUE, 5 );
	$hbox4->pack_start( $angle_sbutton, TRUE,  TRUE, 5 );

	$hbox_row2->pack_start_defaults( $hbox4 );
}


$vbox_param->pack_start( $hbox_row2, TRUE, TRUE, 5 );

$vbox_main->pack_start( $vbox_param, FALSE, TRUE, 5 );

$vbox_main->pack_start( $preview, TRUE, TRUE, 5 );
$vbox_main->pack_start( $preview_throb, TRUE, TRUE, 5 );
$vbox_main->pack_start( $refresh_btn, TRUE, TRUE, 5 );

$hbox_btn->pack_start( $cancel_btn, TRUE, TRUE, 5 );
$hbox_btn->pack_start( $save_btn,   TRUE, TRUE, 5 );
$vbox_main->pack_start( $hbox_btn,  TRUE, TRUE, 5 );

$plug->add( $vbox_main );

$plug->show_all;

#hide the preview widget at startup 
$preview->hide_all;

#create tempfile
my ( $tmpfh, $tmpfilename ) = tempfile();
#png format
$tmpfilename .= ".png";

#we fork a child process to do the work
my $process = Proc::Simple->new;
#create tempfiles for subprocess outputs
my ( $tmpfh_stdout, $tmpfilename_stdout ) = tempfile(UNLINK => 1);
my ( $tmpfh_stderr, $tmpfilename_sterr ) = tempfile(UNLINK => 1);
$process->redirect_output ($tmpfilename_stdout, $tmpfilename_sterr);

#generate first preview at startup
Glib::Idle->add (
	sub{
		&fct_imagemagick_polaroid( undef, 'refresh' );
		return FALSE;	
	}, 
undef);

#lets'start
Gtk2->main;

####define your functions here


sub fct_imagemagick_polaroid {
	my ( $widget, $data ) = @_;

	$save_btn->set_sensitive(FALSE);

	if ( $data eq 'save' ) {

		my $image = Image::Magick->new;
		$image->ReadImage( $tmpfilename );
		$image->WriteImage( filename => $filename );

		#delete temp files
		unlink $tmpfilename;

		Gtk2->main_quit;
		return TRUE;
	} else {

		$preview->hide_all;
		$preview_throb->show_all;

		$process->start(
			sub {
				&apply_effect();
				POSIX::_exit(0);
			}
		);

		#so we can update the gui
		while ( $process->poll ) {
			&fct_update_gui;
			usleep 100000;
		}
	
		eval{
			$preview->set_from_pixbuf( Gtk2::Gdk::Pixbuf->new_from_file_at_scale(
										$tmpfilename, 300, 300, TRUE)
								 	 );
		};
		#error -> read output from tempfiles
		if($@){		
			#store error here
			my $error_string = "Plugin error:\n";
			
			#reading stdout from file
			while (<$tmpfh_stdout>){
				$error_string .= $_;	
			}
			#reading stderr from file
			while (<$tmpfh_stderr>){
				$error_string .= $_;	
			}
			
			#get the parent window of the plug
			require X11::Protocol;
			my $x11 = X11::Protocol->new( $ENV{ 'DISPLAY' } );
			
			my $plugp = Gtk2::Gdk::Window->foreign_new( &find_wm_window( $x11, $plug->get_id ));
			
			#show error message
			my $shutter_dialog = Shutter::App::SimpleDialogs->new($plug, $plugp);
			$shutter_dialog->dlg_error_message( 
				sprintf ( $d->get(  "Error while executing plugin %s." ), "'" . $plugin_info{'name'} . "'" ) ,
				$d->get( "There was an error executing the plugin." ),
				undef, undef, undef,
				undef, undef, undef,
				$error_string
			);
			
			#delete temp files
			unlink $tmpfilename;

			Gtk2->main_quit;
			exit 1;	
		}

		$save_btn->set_sensitive(TRUE);
		$preview->show_all;
		$preview_throb->hide_all;

		return TRUE;
	}
}

sub apply_effect {

	#quote filename
	my $qfilename = quotemeta $filename;

	if ( $use_caption ) {

		my $text = $caption_entry->get_text;
		utf8::decode $text;
		$text = strftime $text, localtime;
		$text = quotemeta $text;

		my $color = $stroke_color->get_color;
			
		system( "convert -caption "
				. $text
				. " -fill '"
				. sprintf( "#%04x%04x%04x%04x", $color->red, $color->green, $color->blue, $stroke_color->get_alpha )
				. "' $qfilename -pointsize "
				. $pointsize_sbutton->get_value
				. " -gravity "
				. $gravity_combo->get_active_text
				. "  -bordercolor snow "
				. " -background black "
				. " -polaroid "
				. $angle_sbutton->get_value
				. " $tmpfilename" );
				
	} else {
		
		system( "convert $qfilename -bordercolor white -border 6 -bordercolor grey60 -border 1 -background none -rotate "
				. $angle_sbutton->get_value
				. " -background  black  \\( +clone -shadow 60x4+4+4 \\) +swap -background none -flatten $tmpfilename" );
	
	}

}

sub fct_update_gui {

	while ( Gtk2->events_pending ) {
		Gtk2->main_iteration;
	}
	Gtk2::Gdk->flush;

	return TRUE;
}

sub find_wm_window {
	my $x11  = shift;
	my $xid  = shift;

	do {
		my ( $qroot, $qparent, @qkids ) = $x11->QueryTree($xid);
		return undef unless ( $qroot || $qparent );
		return $xid if ( $qroot == $qparent );
		$xid = $qparent;
	} while (TRUE);
}

1;

