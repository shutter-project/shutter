#! /usr/bin/env perl

###################################################
#
#  Copyright (C) 2008-2012 Mario Kemper <mario.kemper@googlemail.com> and Shutter Team
#
#  This file is part of Shutter.
#
#  Shutter is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 3 of the License, or
#  (at your option) any later version.
#
#  Shutter is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with Shutter; if not, write to the Free Software
#  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
#
###################################################

use utf8;
use strict;
use warnings;
use Gtk2 '-init';
use Image::Magick;
use POSIX qw/setlocale/;
use Locale::gettext;
use Glib qw/TRUE FALSE/;
use FindBin '$Bin';    #path where plugin is located
use File::Temp qw/ tempfile tempdir /;
use Time::HiRes qw/usleep/;

#load modules at custom path at runtime
#--------------------------------------
require lib;
import lib $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/modules";

#proc (Thanks to Michael Schilli)
require Proc::Simple;

#load shutter's modules
#--------------------------------------
require Shutter::App::SimpleDialogs;

#Set LC_NUMERIC to C to prevent decimal commas (or anything else)
setlocale(LC_NUMERIC, "C");	
#configure gettext using ENV Variable (setup during shutter start)
setlocale( LC_MESSAGES, "" );
my $d = Locale::gettext->domain("shutter-plugins");
$d->dir( $ENV{'SHUTTER_INTL'} );

#icontheme to determine if icons exist or not
#in some cases we deliver fallback icons
my $icontheme = Gtk2::IconTheme->get_default;
$icontheme->append_search_path($ENV{'SHUTTER_ROOT'} . "/share/icons");

#shutter will ask for some infos
my %plugin_info = 	( 	'name'	=> $d->get( "Negate" ),
						'sort'	=> $d->get( "Effect" ),
						'tip'	=> $d->get("Invert the colors of the image"),
					);

binmode( STDOUT, ":utf8" );
if ( exists $plugin_info{$ARGV[ 0 ]} ) {
	print $plugin_info{$ARGV[ 0 ]};
	exit;
}

#these variables are passed to the plugin
my $socket_id = $ARGV[0];
my $filename  = $ARGV[1];
my $width     = $ARGV[2];
my $height    = $ARGV[3];
my $filetype  = $ARGV[4];

#decode filename
utf8::decode $filename;

my $plug = Gtk2::Plug->new( $socket_id );
$plug->set_default_icon_name( 'shutter' );
$plug->set_border_width( 10 );;

$plug->signal_connect( destroy => sub { Gtk2->main_quit } );

#variables used in this plugin
my $width_preview  = 0;
my $height_preview = 0;

#define the gui layout
my $hbox      = Gtk2::HBox->new( 0, 10 );
my $hbox_btn  = Gtk2::HBox->new( 0, 10 );
my $vbox_lbl  = Gtk2::VBox->new( 0, 10 );
my $vbox_btn  = Gtk2::VBox->new( 0, 10 );
my $vbox_main = Gtk2::VBox->new( 0, 10 );

#configure buttons and other needed controls
my $negate_label = Gtk2::Label->new( $d->get("Channel") );

my $negate_channel = Gtk2::ComboBox->new_text;
$negate_channel->insert_text( 0, "All" );
$negate_channel->insert_text( 1, "Alpha" );
$negate_channel->insert_text( 2, "Black" );
$negate_channel->insert_text( 3, "Blue" );
$negate_channel->insert_text( 4, "Cyan" );
$negate_channel->insert_text( 5, "Gray" );
$negate_channel->insert_text( 6, "Green" );
$negate_channel->insert_text( 7, "Index" );
$negate_channel->insert_text( 8, "Magenta" );
$negate_channel->insert_text( 9, "Opacity" );
$negate_channel->insert_text( 10, "Red" );
$negate_channel->insert_text( 11, "Yellow" );
$negate_channel->set_active(0);

#we define two Gtk2::Image widgets
#to store the screenshot
#and a throbber that is shown while the changes are processed
my $preview =
	Gtk2::Image->new_from_pixbuf(
	   Gtk2::Gdk::Pixbuf->new_from_file_at_scale( $filename, 300, 300, TRUE ) );

my $preview_throb =
	Gtk2::Image->new_from_file( $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/icons/throbber.gif" );

my $sg = Gtk2::SizeGroup->new ('both');
$sg->set ('ignore-hidden' => FALSE);
$sg->add_widget($preview);
$sg->add_widget($preview_throb);


#we define three Gtk2::Button widgets
#to refresh, save and cancel the plugin's work
my $refresh_btn = Gtk2::Button->new_from_stock( 'gtk-refresh' );
$refresh_btn->signal_connect( 'clicked', \&fct_imagemagick_negate,
							  'refresh' );

my $save_btn = Gtk2::Button->new_from_stock( 'gtk-save' );
$save_btn->signal_connect( 'clicked', \&fct_imagemagick_negate, 'save' );

my $cancel_btn = Gtk2::Button->new_from_stock( 'gtk-cancel' );
$cancel_btn->signal_connect( 'clicked' => sub { Gtk2->main_quit; exit 2; }, 'cancel' );

#packing
$hbox->pack_start( $negate_label, FALSE, TRUE, 0 );
$hbox->pack_start( $negate_channel, TRUE, TRUE, 0 );

$hbox_btn->pack_start( $cancel_btn, TRUE, TRUE, 0 );
$hbox_btn->pack_start( $save_btn, TRUE, TRUE, 0 );

$vbox_main->pack_start( $hbox, TRUE, TRUE, 0 );
$vbox_main->pack_start( $preview, TRUE, TRUE, 0 );
$vbox_main->pack_start( $preview_throb, TRUE, TRUE, 0 );
$vbox_main->pack_start( $refresh_btn, TRUE, TRUE, 0 );
$vbox_main->pack_start( $hbox_btn, TRUE, TRUE, 0 );

$plug->add($vbox_main);

$plug->show_all;

#hide the preview widget at startup 
$preview->hide_all;

#create tempfile
my ( $tmpfh, $tmpfilename ) = tempfile();
#png format
$tmpfilename .= ".png";

#we fork a child process to do the work
my $process = Proc::Simple->new;
#create tempfiles for subprocess outputs
my ( $tmpfh_stdout, $tmpfilename_stdout ) = tempfile(UNLINK => 1);
my ( $tmpfh_stderr, $tmpfilename_sterr ) = tempfile(UNLINK => 1);
$process->redirect_output ($tmpfilename_stdout, $tmpfilename_sterr);

#generate first preview at startup
Glib::Idle->add (
	sub{
		&fct_imagemagick_negate( undef, 'refresh' );
		return FALSE;	
	}, 
undef);

#lets'start
Gtk2->main;

####define your functions here
sub value_changed {
	my ( $widget, $data ) = @_;

	return TRUE;
}

sub fct_imagemagick_negate {
	my ( $widget, $data ) = @_;

	$save_btn->set_sensitive(FALSE);

	if ( $data eq 'save' ) {
		
		my $image = Image::Magick->new;
		$image->ReadImage($tmpfilename);
		$image->WriteImage( filename => $filename );

		#delete temp files
		unlink $tmpfilename;

		Gtk2->main_quit;
		return TRUE;
	}
	else {
		
		$preview->hide_all;
		$preview_throb->show_all;

		$process->start(
			sub {
				&apply_effect();
				POSIX::_exit(0);
			}
		);

		#so we can update the gui
		while ( $process->poll ) {
			&fct_update_gui;
			usleep 100000;
		}

		eval{
			$preview->set_from_pixbuf( Gtk2::Gdk::Pixbuf->new_from_file_at_scale(
										$tmpfilename, 300, 300, TRUE)
								 	 );
		};
		#error -> read output from tempfiles
		if($@){		
			#store error here
			my $error_string = "Plugin error:\n";
			
			#reading stdout from file
			while (<$tmpfh_stdout>){
				$error_string .= $_;	
			}
			#reading stderr from file
			while (<$tmpfh_stderr>){
				$error_string .= $_;	
			}
			
			#get the parent window of the plug
			require X11::Protocol;
			my $x11 = X11::Protocol->new( $ENV{ 'DISPLAY' } );
			
			my $plugp = Gtk2::Gdk::Window->foreign_new( &find_wm_window( $x11, $plug->get_id ));
			
			#show error message
			my $shutter_dialog = Shutter::App::SimpleDialogs->new($plug, $plugp);
			$shutter_dialog->dlg_error_message( 
				sprintf ( $d->get(  "Error while executing plugin %s." ), "'" . $plugin_info{'name'} . "'" ) ,
				$d->get( "There was an error executing the plugin." ),
				undef, undef, undef,
				undef, undef, undef,
				$error_string
			);
			
			#delete temp files
			unlink $tmpfilename;

			Gtk2->main_quit;
			exit 1;	
		}

		$save_btn->set_sensitive(TRUE);
		$preview->show_all;
		$preview_throb->hide_all;

		return TRUE;
	}
}

sub apply_effect {	

	my $image = Image::Magick->new;
	$image->ReadImage($filename);
	$image->Negate(channel => $negate_channel->get_active_text);
	$image->WriteImage( filename => $tmpfilename );
	
}	

#~ sub fct_imagemagick_to_pixbuf {
	#~ my ($blob) = @_;
	#~ my $pixbufloader = Gtk2::Gdk::PixbufLoader->new;
	#~ $pixbufloader->set_size( $width_preview, $height_preview );
	#~ $pixbufloader->write($blob);
	#~ $pixbufloader->close;
	#~ my $pixbuf = $pixbufloader->get_pixbuf;
#~ 
	#~ return $pixbuf;
#~ }

sub fct_update_gui {

	while ( Gtk2->events_pending ) {
		Gtk2->main_iteration;
	}
	Gtk2::Gdk->flush;

	return TRUE;
}

sub find_wm_window {
	my $x11  = shift;
	my $xid  = shift;

	do {
		my ( $qroot, $qparent, @qkids ) = $x11->QueryTree($xid);
		return undef unless ( $qroot || $qparent );
		return $xid if ( $qroot == $qparent );
		$xid = $qparent;
	} while (TRUE);
}

1;

